selector
{
	behavior use_medkit

	sequence
	{
		condition alertedToEnemy
		selector
		{
			decorator timer ( 1000 )
			{
				condition haveWeapon( WP_LUCIFER_CANNON ) && distanceTo( E_H_REACTOR ) <= 400
				{
					action buy( WP_PULSE_RIFLE )
				}
			}
			condition haveWeapon( WP_HBUILD ) && ( !buildingIsDamaged || teamateHasWeapon( WP_HBUILD ) )
			{
				selector
				{
					action equip
					action flee
				}
			}

			decorator timer( 1000 )
			{
				selector
				{
					// defending reactor, or try to avoid being killed while healing (hence the different values)
					// the isVisible check is here to prevent "defending" when enemies are detected by radars
					condition ( ( distanceTo( E_H_REACTOR ) <= 300 || distanceTo( E_H_MEDISTAT ) <= 250 ) && isVisible( E_ENEMY ) )
					{
						action fight
					}

					sequence
					{
						condition ( healScore > 0.5 && percentHealth( E_GOAL ) > 0.3  && random > 0.3 )
						action heal
					}
				}
			}

			sequence
			{
				//if goal is a close enough enemy building or if we're gonna die
				condition ( percentHealth( E_SELF ) < 0.3 || ( distanceTo( E_GOAL ) < 400 && goalType == ET_BUILDABLE ) )
				condition ( distanceTo( E_FRIENDLYBUILDING ) > 400 )
				selector
				{
					// no need to check the upgrade is in inventory, since action would fail
					action activateUpgrade( UP_FIREBOMB )
					action activateUpgrade( UP_GRENADE )
				}
			}
			action fight
		}
	}

	selector
	{
		sequence
		{
			condition !haveUpgrade( UP_MEDKIT )
			condition healScore > 0.25
			action heal
		}
	}

	sequence
	{
		condition !teamateHasWeapon( WP_HBUILD )
		condition buildingIsDamaged
		decorator timer( 50000 )
		{
			selector
			{
				condition !haveWeapon( WP_HBUILD )
				{
					sequence
					{
						decorator return( STATUS_SUCCESS )
						{
							action equip
						}
						action buy( WP_HBUILD )
					}
				}

				condition haveWeapon( WP_HBUILD )
				{
					action repair
				}
			}
		}
	}

	action equip

	condition baseRushScore > 0.5
	{
		action rush
	}

	action roam
}
