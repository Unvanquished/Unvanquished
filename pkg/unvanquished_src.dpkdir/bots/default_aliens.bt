selector
{
	sequence
	{
		condition numUsersInTeam == 0
		condition buildPointsNonneg >= chosenBuildableCost
		condition !teamateIsClass( PCL_ALIEN_BUILDER0 ) && !teamateIsClass( PCL_ALIEN_BUILDER0_UPG )
		selector
		{
			condition class != PCL_ALIEN_BUILDER0 && class != PCL_ALIEN_BUILDER0_UPG
			{
				selector
				{
					sequence
					{
						action moveTo( E_A_OVERMIND )
						selector
						{
							action evolveTo( PCL_ALIEN_BUILDER0_UPG )
							action evolveTo( PCL_ALIEN_BUILDER0 )
						}
					}
					action suicide
				}
			}

			condition class == PCL_ALIEN_BUILDER0 || class == PCL_ALIEN_BUILDER0_UPG
			{
				selector
				{
					decorator return( STATUS_FAILURE )
					{
						action buildNowChosenBuildable
					}
					action roamInRadius( E_A_OVERMIND, 700 )
					action roam
				}
			}
		}
	}

	condition ( aliveTime > 1500 && healScore < 0.5 )
	{
		action evolve
	}

	sequence
	{
		condition alertedToEnemy
		selector
		{
			decorator timer( 1000 )
			{
				selector
				{
					sequence
					{
						condition alertedToEnemy
						condition ( distanceTo( E_A_OVERMIND ) <= 100 || distanceTo( E_A_BOOSTER ) <= 100 )
						action fight
					}
					sequence
					{
						condition ( healScore > 0.5 && percentHealth( E_GOAL ) > 0.3  && random > 0.3 )
						condition baseRushScore < 0.75
						action heal
					}
				}
			}

			action fight
		}
	}

	selector
	{
		sequence
		{
			condition healScore > 0.25
			action heal
		}
	}

	condition baseRushScore > 0.5
	{
		action rush
	}

	action roam
}
