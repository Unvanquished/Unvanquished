selector
{
	sequence
	{
		condition levelTime >= 60000
		condition numUsersInTeam == 0 || numOurBuildings( E_A_SPAWN ) < 2
		condition usableBuildPoints >= chosenBuildableCost
		condition !teamateHasWeapon( WP_ABUILD ) && !teamateHasWeapon( WP_ABUILD2 )
		condition blackboardNumTransient( 1 ) < 1
		selector
		{
			decorator return ( STATUS_FAILURE )
			{
				action blackboardNoteTransient( 1 )
			}

			sequence
			{
				condition class != PCL_ALIEN_BUILDER0 && class != PCL_ALIEN_BUILDER0_UPG
				selector
				{
					condition timeSinceMyTimerUpdate >= 20000
					{
						action suicide
					}
					action evolveTo( PCL_ALIEN_BUILDER0_UPG )
					action evolveTo( PCL_ALIEN_BUILDER0 )
					action roamInRadius( E_A_OVERMIND, 500 )
					condition alertedToEnemy
					{
						action fight
					}
					action rush
					action roam
				}
			}

			decorator return( STATUS_FAILURE )
			{
				action buildNowChosenBuildable
			}
			action roamInRadius( E_A_OVERMIND, 700 )
			action roam
		}
	}

	decorator return ( STATUS_FAILURE )
	{
		action updateMyTimer
	}

	condition ( aliveTime > 1500 && healScore < 0.5 )
	{
		action evolve
	}

	sequence
	{
		condition alertedToEnemy
		selector
		{
			decorator timer( 1000 )
			{
				selector
				{
					sequence
					{
						condition alertedToEnemy
						condition ( distanceTo( E_A_OVERMIND ) <= 100 || distanceTo( E_A_BOOSTER ) <= 100 )
						action fight
					}
					sequence
					{
						condition ( healScore > 0.5 && percentHealth( E_GOAL ) > 0.3  && random > 0.3 )
						condition baseRushScore < 0.75
						action heal
					}
				}
			}

			action fight
		}
	}

	selector
	{
		sequence
		{
			condition healScore > 0.25
			action heal
		}
	}

	condition baseRushScore > 0.5
	{
		action rush
	}

	action roam
}
