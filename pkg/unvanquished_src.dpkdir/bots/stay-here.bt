selector
{
	behavior subroutine_unstick

	condition team == TEAM_HUMANS
	{
		selector
		{
			behavior subroutine_use_medkit

			sequence
			{
				condition alertedToEnemy
				selector
				{
					decorator timer( 1000 )
					{
						selector
						{
							// defending reactor, or try to avoid being killed while healing (hence the different values)
							// the isVisible check is here to prevent "defending" when enemies are detected by radars
							condition ( ( distanceTo( E_H_REACTOR ) <= 300 || distanceTo( E_H_MEDISTAT ) <= 250 ) && isVisible( E_ENEMY ) )
							{
								action fight
							}

							sequence
							{
								condition ( healScore > 0.5 && percentHealth( E_GOAL ) > 0.3  && random > 0.3 )
								condition baseRushScore < 0.75
								behavior subroutine_heal_humans
							}
						}
					}

					behavior subroutine_throw_grenade
					action fight
				}
			}

			sequence
			{
				condition healScore > 0.25
				behavior subroutine_heal_humans
			}

			behavior subroutine_reload
			sequence
			{
				condition ( distanceTo( E_H_ARMOURY ) < 500 )
				action equip
			}

			action stayHere( 500 )
			action roam
		}

	}

	condition team == TEAM_ALIENS
	{
		selector
		{
			behavior subroutine_extinguish_fire

			condition ( aliveTime > 1500 && healScore < 0.5 )
			{
				action evolve
			}

			behavior subroutine_fight_or_flight_aliens

			action stayHere( 500 )
			action roam
		}
	}
}
